file = { SOI ~ NEWLINE* ~ line ~ (NEWLINE+ ~ line)* ~ NEWLINE* ~ EOI }

line = _{ a_instruction | c_instruction | line_other }
line_other = @{ (!NEWLINE ~ ANY)+ }

a_instruction = { "@" ~ constant }
constant = @{ ASCII_DIGIT+ }

c_instruction = ${ (dest ~ "=")? ~ comp ~ (";" ~ jump)? }
dest = @{ ("A" | "D" | "M")+ }
comp = @{
    "0" | "1" | "-1"
    | "D" ~ ("+" | "-" | "&" | "|") ~ a_or_m
    | a_or_m ~ "-" ~ "D"
    | register ~ ("+" | "-") ~ "1"
    | ("!" | "-")? ~ register
}
register = @{ "A" | "D" | "M" }
a_or_m = @{ "A" | "M" }
jump = { "JGT" | "JEQ" | "JGE" | "JLT" | "JNE" | "JLE" | "JMP" }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }
